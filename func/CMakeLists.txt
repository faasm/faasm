cmake_minimum_required(VERSION 3.8.0)
project(faasm-func)

set(CMAKE_CXX_STANDARD 17)

if (FAASM_BUILD_TYPE STREQUAL "lambda-func")
    add_definitions(-DAWS_LAMBDA=1)

    # Add faasm lambda headers
    include_directories(${FAASM_LAMBDA_LIB_DIR})
else ()
    add_definitions(-DAWS_LAMBDA=0)
endif ()

if (FAASM_BUILD_TYPE STREQUAL "emscripten")
    message(STATUS "Detected emscripten build")

    add_definitions(-DWASM_BUILD=1)

    # -------- NOTE ----------
    # The stack-first here is really important to help detect stack overflow
    # issues. Without it the stack will overflow into the global data.
    # stack-size is also crucial to bigger functions not messing up
    SET(CMAKE_EXE_LINKER_FLAGS "-Xlinker --no-entry \
    -Xlinker --export=main \
    -Xlinker --max-memory=1073741824 \
    -Xlinker --stack-first \
    -Xlinker --threads \
    -Wl,-z,stack-size=1048576 -Wl, \
    ")

    SET(CMAKE_EXECUTABLE_SUFFIX ".wasm")
endif ()


# Define functions for building
if (FAASM_BUILD_TYPE STREQUAL "emscripten")
    # ----------------------
    # Emscripten build
    # ----------------------

    function(faasm_func exec_name file_name)
        add_executable(${exec_name} ${file_name})
        target_link_libraries(${exec_name} faasm dlmalloc)
    endfunction(faasm_func)

elseif (FAASM_BUILD_TYPE STREQUAL "lambda-func")
    # ----------------------
    # AWS Lambda build
    # ----------------------
    function(faasm_func func_name file_name)
        add_executable(${func_name}-lambda ${file_name})
        target_link_libraries(${func_name}-lambda lambda-func)

        # Add custom target to build Lambda zip file
        aws_lambda_package_target(${func_name}-lambda)
    endfunction(faasm_func)

else ()
    # ----------------------
    # Native build
    # ----------------------

    function(faasm_func exec_name file_name)
        add_executable(${exec_name} ${file_name})
        target_link_libraries(${exec_name} faasm emulator dl)
    endfunction(faasm_func)

endif ()

add_subdirectory(errors)
add_subdirectory(demo)
add_subdirectory(sgd)

# Add python build in everything but the lambda funcs
if (FAASM_BUILD_TYPE STREQUAL "lambda-func")
else ()
    add_subdirectory(python)
endif ()